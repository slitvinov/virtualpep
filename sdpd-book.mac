tr(i, j):= SU(dW(i, j, s, s), [s, 1, ndim]);
wave(i, j, al, be):=1/2*(dW(i,j,al,be) + dW(i,j,be,al)) - kron_delta(al,be)/ndim*tr(i, j);

dvdv_book: kron_delta(i,j)*sum(
    A(i,k)^2/2*(kron_delta(al,be)+ee(i,k,al)*ee(i,k,be)),
    k, 1, Np)
+         kron_delta(i,j)*sum(
    (B(i,k)^2-A(i,k)^2)/ndim*ee(i,k,al)*ee(i,k,be),
    k, 1, Np)
-   A(i,j)^2/2*(kron_delta(al,be) + ee(i,j,al)*ee(i,j,be))
-   (B(i,j)^2-A(i,j)^2)/ndim*ee(i,j,al)*ee(i,j,be);

edv2al(i,k):= sum(ee(i,k,be)*vv(i,k,be)/2, be, 1, ndim)*ee(i,k,al);
dvds_book: - (
kron_delta(i,j)*sum(
  A(i,k)^2/2*(vv(i,k,al)/2 + edv2al(i,k)),
    k, 1, Np)
+         kron_delta(i,j)*sum(
    (B(i,k)^2-A(i,k)^2)/ndim*edv2al(i,k),
    k, 1, Np)
+   A(i,j)^2/2*(vv(i,j,al)/2 + edv2al(i,j))
+   (B(i,j)^2-A(i,j)^2)/ndim*edv2al(i,j)
);

dsdv_book: - (
kron_delta(i,j)*sum(
  A(i,k)^2/2*(vv(i,k,al)/2 + edv2al(i,k)),
    k, 1, Np)
+         kron_delta(i,j)*sum(
    (B(i,k)^2-A(i,k)^2)/ndim*edv2al(i,k),
    k, 1, Np)
-   A(i,j)^2/2*(vv(i,j,al)/2 + edv2al(i,j))
-   (B(i,j)^2-A(i,j)^2)/ndim*edv2al(i,j)
);
kill(edv2al);

vv2(i,k):=sum(vv(i,k,al)^2, al, 1, ndim)/2^2;
edv2(i,k):=sum(ee(i,k,al)*vv(i,k,al)/2, al, 1, ndim)^2;
dsds_book: kron_delta(i,j)*sum(
  A(i,k)^2/2*(vv2(i,k) + edv2(i,k)),
    k, 1, Np)
+         kron_delta(i,j)*sum(
    (B(i,k)^2-A(i,k)^2)/ndim*edv2(i,k),
    k, 1, Np)
+   A(i,j)^2/2*(vv2(i,j) + edv2(i,j))
+   (B(i,j)^2-A(i,j)^2)/ndim*edv2(i,j)
+   kron_delta(i,j)*sum(C(i,k)^2, k, 1, Np) - C(i,j)^2;
kill(vv2, edv2);

/* equation (B5)  */
dvds_diff: sum( (d(i,j) * (
      a(i,j)*vv(i,j,al) +
            (a(i,j)/3 + b(i,j))*sum(vv(i,j,be)*ee(i,j,be), be, 1, ndim)*ee(i,j,al)
      )
    ), j, 1, Np) / m;

/* equation (B8) */
dsdv_diff: -2*kb/(m*T(i)) * sum( ( (10/3*a(i,j) + b(i,j)) * T(i)*T(j)/(T(i)+T(j)) )* (1-kron_delta(i,j)),
  j, 1, Np);

/*
equation (B9), kappa, Fij, di, dj are substituted from the last line
of (61)
cij = 2*kapp*Fij/(di*dj)

*/
dsds_diff: -1/T(i)*kb/Cs(i)*sum(c(i,j)*T(j), j, 1, Np)
- 1/(2*T(i))*sum( ( T(j)/(T(i)+T(j))*kb/Cs(i) + d(i,j) )*
  (a(i,j)*sum(vv(i,j,al)^2, al, 1, ndim) + (a(i,j)/3+b(i,j))*sum(vv(i,j,al)*ee(i,j,al),al,1,ndim)^2),
  j, 1, Np)$

/* define substitution (60) */
eq_def: [
   a(i,j) = A(i,j)^2/(8*kb)*(1/T(i) + 1/T(j)),
   b(i,j) = B(i,j)^2/(12*kb)*(1/T(i) + 1/T(j)),
   c(i,j) = C(i,j)^2/(2*kb*T(i)*T(j)),
   d(i,j) = T(i)*T(j)/(T(i)+T(j))^2*(kb/Cs(i)+kb/Cs(j))
]$

/* small -> big */
small_def: [
   subst(rhs(solve(eq_def[1], a(i,j))[1]), g, a = lambda([i,j], g)),
   subst(rhs(solve(eq_def[2], b(i,j))[1]), g, b = lambda([i,j], g)),
   subst(rhs(solve(eq_def[3], c(i,j))[1]), g, c = lambda([i,j], g)),
   subst(rhs(solve(eq_def[4], d(i,j))[1]), g, d = lambda([i,j], g))
   ]$

/* big -> small */
big_def: [
   subst(rhs(solve(eq_def[1], A(i,j))[2]), g, A = lambda([i,j], g)),
   subst(rhs(solve(eq_def[2], B(i,j))[2]), g, B = lambda([i,j], g)),
   subst(rhs(solve(eq_def[3], C(i,j))[2]), g, C = lambda([i,j], g))
]$

/* LHS of equation (B2) */
b2_lhs: (kron_delta(i,j)*kron_delta(ip,jp)+kron_delta(i,jp)*kron_delta(ip,j)) * (
             A(i,ip)*A(j,jp)*1/2*(
                 kron_delta(al,be)*kron_delta(alp,bep)+kron_delta(alp,be)*kron_delta(al,bep)
                 )
             +(B(i,ip)*B(j,jp)-A(i,ip)*A(j,jp))*1/3*kron_delta(al,alp)*kron_delta(be,bep)
)$
